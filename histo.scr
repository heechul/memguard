reset
n=100 #number of intervals
max=18.0 #max value
min=8.0 #min value
width=(max-min)/n #interval width
#function used to map a value to the intervals
hist(x,width)=width*floor(x/width)+width/2.0
set terminal postscript eps enhanced
#set output "histogram.eps"
set xrange [min:max]
set yrange [0:500]
#to put an empty boundary around the
#data inside an autoscaled graph.
set offset graph 0.05,0.05,0.05,0.0
set xtics min,(max-min)/5,max
set boxwidth width*0.9
set style fill solid 0.5 #fillstyle
set tics out nomirror
set xlabel "Latency(ms)"
set ylabel "Frequency"
set arrow from 13,-10 to 13,510 nohead lc rgb 'red'
#count and plot
#plot "solo.dat" u (hist($2,width)):(1.0) smooth freq w boxes lc rgb"green" notitle
plot "rawdata.dat" u (hist($2,width)):(1.0) smooth freq w boxes lc rgb"green" notitle
